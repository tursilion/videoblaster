* F18A GPU version - writes to an offscreen
* buffer, then relies on the GPU to copy it
* much more quickly to display memory, avoiding
* most if not all of the visible glitching
*
* Interleaved 8-bit code:
* 8-bit - 336578 cycles per frame
* Each frame has 6144 bytes of video, and 1544 bytes of audio.
* approximate output is: 
* 8.9132 fps and 13762.04 Hz (but audio is best calculated
* as (frames * 6144) = samples / duration = rate)

SOUND	EQU	>8400
VDPWA	EQU	>8C02
VDPWD	EQU >8C00
VDPRD	EQU >8800

	DEF MAIN

source equ >7FFF	* fake hardware for the moment
*					* should reset when >6000 is read
MAIN
	LIMI 0
	LWPI >8300
	
*	* set up sound chip
	LI R0,>9FBF		* mute all channels
	MOVB R0,@SOUND
	SWPB R0
	MOVB R0,@SOUND
	LI R0,>DFFF
	MOVB R0,@SOUND
	SWPB R0
	MOVB R0,@SOUND
	LI R0,>8100		* highest frequency
	MOVB R0,@SOUND
	SWPB R0
	MOVB R0,@SOUND

* test for F18A, and unlock. 
* for now, reset if it's not there.
* This is not the way Matthew intended, but it's a tad easier
* We just check if the GPU is present by having it run a short
* program, instead of mucking around with status bits.
* F18A blind unlock code
	LI R0,>B91C             * VR1/57, value 00011100
	BL @VDPWTA              * write once (corrupts VDPR1)
	BL @VDPWTA              * write again (unlocks enhanced mode)

* If we are not on the F18A, then R1 is screwed up now, that's okay
	LI R0,>5C00
	BL @VDPWTA
	LI R1,GPUTEST
	LI R2,10
TSTLP
	MOVB *R1+,@VDPWD		* copy program
	DEC R2
	JNE TSTLP
	
	LI R0,>5C10				* test address
	BL @VDPWTA
	CLR @VDPWD				* clear it
	CLR @VDPWD
	
* Start the GPU program, pause, then check the result
	LI R0,>B61C				* MSB
	BL @VDPWTA
	LI R0,>B700				* LSB and start
	BL @VDPWTA
	NOP						* give it a moment - it's WAY faster than we are
	LI R0,>1C10
	BL @VDPWTA
	NOP
	MOVB @VDPRD,R1			* read the data
	SWPB R1
	MOVB @VDPRD,R1
	CI R1,>3412
	JEQ F18YES
	BLWP @>0000				* no F18A, so reset

F18YES

* good, so copy the GPU program in and start it
	LI R0,>5B80
	BL @VDPWTA
	LI R1,GPUGO
GLDLP
	MOVB *R1+,@VDPWD
	CI R1,GPUEND
	JNE GLDLP
	
* start running (it will move itself into high memory)
	LI R0,>B61B				* MSB
	BL @VDPWTA
	LI R0,>B780				* LSB and start
	BL @VDPWTA
	
*	* set up bitmap
	BL @BITMAP
	
*	* set up copy

* Theory of op:
* This uses 4 copies like the original code, and has the same
* timing and tearing limitations. The difference is that
* each block is copied to 'unused' video RAM, and a command
* written to the polling GPU program.
* the GPU then copies the block as instructed.
* First, the CPU writes the pattern table, then toggles the
* GPU. The GPU copies the pattern data to a block in high
* memory (mostly because we're out of memory in the main
* VDP space, and we can afford the time).
* The CPU then (possibly starting overlapped) copies the
* color table to the buffer. The command next written
* instructs the GPU to copy both pattern and color to the
* correct place for display. Because the GPU has random access
* to VDP memory, it can alternate writes for minimum time
* between updates. Because it's so blinding fast, it should
* easily finish the update before the next block is ready.
* the result should be nearly flicker-free video.
*
* So, the 9900 always writes starting at >1B80,
* it then writes one extra byte to >1E80, containg a GPU
* command:
* 0x80	-	copy to second buffer
* 0x20	- 	copy first blocks
* 0x28	-	copy second blocks
* 0x2B	- 	copy third blocks
* 0x30	-	copy fourth blocks

	li r2,>805B		* start address buffer >1B80
	li r3,>8000		* copy to second buffer
	li r4,>2000		* copy first blocks
	li r5,>2800		* copy second blocks
	li r6,>2B00		* copy third blocks
	li r7,>3000		* copy fourth blocks
	li r10,192		* size of one table divided by 4 (24*4*8/4)
	li r12,VDPWA
	LI r13,VDPWD
	li r14,source
	li r15,SOUND

	mov r2,r0
	bl @VDPWTA		* set to buffer address so the first cmd write
*					* doesn't end up in a random place (will not trigger GPU)
	
* try to keep the cycles between sound writes about even
* the setup blocks need to be 2 cycles longer than the copy blocks
* to make up for the JNE fallthrough being 2 cycles shorter.
*
* Although the smaller code needs fewer bytes to VDP, to keep the
* code reasonably tight, we now need SIX copy loops.
* 
* The data pattern needs to be (P=PATTERN, C=COLOR, S=Sound):
* 1		S
* 192	PPPPS	first 1/3rd, chars 0-95
* 1		S
* 192	CCCCS
* 1		S
* 192	PPPPS	second 1/3rd, chars 0-95
* 1		S
* 192	CCCCS
* 1		S
* 192	PPPPS	second 1/3rd, chars 96-191
* 1		S
* 192	CCCCS
* 1		S
* 192	PPPPS	third 1/3rd, chars 0-95
* 1		S
* 192	CCCCS
*
* Repeat - above sequence is first pattern data, then color data (color at >2000)
* We are interleaving the pattern and color through each table
*
*                                                  8Bit	16bit 
OUTLOOP

* Pattern 1
	MOVB R7,*R13	* command to copy fourth block	30  26
	SRL R0,8		* waste time					32	28
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192
LOOP1
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP1										14	10
*												   218 190

* Color 1
	MOVB R3,*R13	* command to copy to buffer		30  26
	NOP												14	10
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	SRL R0,8		* waste time					32	28
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192

LOOP1c
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP1c										14	10
*												   218 190

* Pattern 2
	MOVB R4,*R13	* command to copy first block	30  26
	NOP												14	10
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	SRL R0,8		* waste time					32	28
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192
	
LOOP2
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP2										14	10
*												   218 190

* Color 2
	MOVB R3,*R13	* command to copy to buffer		30  26
	NOP												14	10
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	SRL R0,8		* waste time					32	28
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192

LOOP2c
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP2c										14	10
*												   218 190

* Pattern 2b
	MOVB R5,*R13	* command to copy second block	30  26
	NOP												14	10
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	SRL R0,8		* waste time					32	28
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192
	
LOOP2b
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP2b										14	10
*												   218 190

* Color 2b
	MOVB R3,*R13	* command to copy to buffer		30  26
	NOP												14	10
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	SRL R0,8		* waste time					32	28
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192

LOOP2bc
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP2bc										14	10
*												   218 190

* Pattern 3
	MOVB R6,*R13	* command to copy third block	30  26
	NOP												14	10
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	SRL R0,8		* waste time					32	28
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192
	
LOOP3
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP3										14	10
*												   218 190

* Color 3
	MOVB R3,*R13	* command to copy to buffer		30  26
	NOP												14	10
	MOVB R2,*R12									30	26
	SWPB R2											14	10
	MOVB R2,*R12	* VDP address set				30	26
	SWPB R2											14	10
	SRL R0,8		* waste time					32	28
	MOV R10,R0		* bytes to write divided by 4	18	14
	MOVB *R14,*R15	* write sound					38	34
*												   220 192

LOOP3c
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R13	* data							38	34
	MOVB *R14,*R15	* write sound					38	34
	DEC R0											14	10
	JNE LOOP3c										14	10
*												   218 190

	JMP OUTLOOP										14	10

* Setup for normal bitmap mode
* returns with video off - set VDP R1 to E2 to enable (>81E2)
SAVE BSS 2
BITMAP
	MOV R11,@SAVE

* set display and disable sprites
	LI R1,BMREGS
	BL @LOADRG
	
* set up SIT - We first fill with 255 bytes
	LI R0,>5800
	BL @VDPWTA
	LI R0,768
	LI R1,>FF00
LP# 
	MOVB R1,@>8C00
	DEC R0
	JNE LP#
	
* Now special setup for each table - we have 16 rows
* of 24 chars each. 
* First table is 4 rows and so starts at row 4
* Indent is 4 chars on each side, so start at 4
	LI R0,>5884	* address
	CLR R1		* char to write
	LI R2,4		* rows left
	BL @WRITEBK
	
* Second table is 8 rows and starts at row 8
	LI R0,>5904	* address
	CLR R1		* char to write
	LI R2,8		* rows left
	BL @WRITEBK
	
* third table is 4 rows and starts at row 16
	LI R0,>5A04	* address
	CLR R1		* char to write
	LI R2,4		* rows left
	BL @WRITEBK
	
	MOV @SAVE,R11
	B *R11

SAVE2 BSS 2

WRITEBK
	MOV R11,@SAVE2
LPA1	
	BL @VDPWTA	* set address
	LI R3,24	* chars left
LPA2
	MOVB R1,@>8C00
	AI R1,>0100	* next char
	DEC R3
	JNE LPA2	* column loop
	
	AI R0,32	* next row
	DEC R2
	JNE LPA1	* row loop
	
	MOV @SAVE2,R11
	B *R11

* registers for bitmap (and 5B00 is the address of the sprite table)
* background is transparent (the only color never redefined)
* PDT - >0000 -	>17FF
* SIT - >1800 - >1AFF
* SDT - >1800 (unused)
* SAL - >1B00 - >1B7F   (not all used)
* BUF - >1B80 - >1E7F	block buffer
* CMD - >1E80 - >1E81	command to the GPU program (MSB)
*     - >1E82 - >1FFF	382 bytes
* CT  - >2000 - >37FF
* DSR - >3800 - >3FFF
* BUF2- >4000 - >42FF	block buffer temp
* GPU - >4300 -	>47FF	GPU program (not all used)
BMREGS DATA >81E0,>8002,>8206,>83ff,>8403,>8536,>8603,>8700,>5B00,>0000

* load regs list to VDP address, end on >0000 and write >D0 (for sprites)
* address of table in R1 (destroyed)
LOADRG
LOADLP
	MOV *R1+,R0
	JEQ LDRDN
	SWPB R0
	MOVB R0,@>8C02
	SWPB R0
	MOVB R0,@>8C02
	JMP LOADLP
LDRDN
	LI R1,>D000
	MOVB R1,@>8C00
	B *R11

* Write address or register
VDPWTA
	SWPB R0
	MOVB R0,@>8C02
	SWPB R0
	MOVB R0,@>8C02
	B *R11  

* GPU code (loads at >1B80 in VDP, copies itself to >4000)
* to make life easy, position independent code! (VDP
* addresses are hard coded though, that's fine)
GPUGO
	li r0,>1B80+GPURUN-GPUGO
	li r1,>4300
GCPLP
	MOV *r0+,*r1+
	ci r0,>1b80+GPUEND-GPUGO
	jne GCPLP

* now jump to it!	
	B @>4300
	
* this code running at >4300
GPURUN
	clr @>1E80		* zero command
	CLR R0
GPUWAIT
	MOVB @>1E80,R0	* get it
	JEQ GPUWAIT		* nothing yet
	CLR @>1E80		* clear it
	JLT BUFCPY		* copying to second buffer
	
* main buffer copy - copy >4000 to pattern, and >1B80 to color
* R0 contains the color table target address
	MOV R0,R1		* for pattern table
	ANDI R1,>1FFF
	LI R2,>1B80		* R2->R0 for the color table
	LI R3,>4000		* R3->R1 for the pattern table
	LI R4,96		* we can move words instead of bytes
GPUVLP	
	MOV *R2+,*R0+
	MOV *R3+,*R1+
	MOV *R2+,*R0+
	MOV *R3+,*R1+
	MOV *R2+,*R0+
	MOV *R3+,*R1+
	MOV *R2+,*R0+
	MOV *R3+,*R1+
	DEC R4
	JNE GPUVLP
	JMP GPURUN		* go wait again

BUFCPY
	LI R0,>1B80		* 9900 write buffer
	LI R1,>4000		* second buffer target when >80
	LI R2,96		* we can move words instead of bytes
GPUBLP
	MOV *R0+,*R1+
	MOV *R0+,*R1+
	MOV *R0+,*R1+
	MOV *R0+,*R1+
	DEC R2
	JNE GPUBLP
	JMP GPURUN		* go wait again

GPUEND

GPUTEST
	DATA >0200,>1234		LI R0,>1234
	DATA >C800,>1C10		MOV R0,@>1C10
	DATA >0340				IDLE

	END
	